// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.32"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28.3-alpha'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.1"
    }
    repositories {
        google()
    }
}

allprojects {

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }
}


subprojects {
    if (project.name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    apply plugin: "kotlin-android"
    apply plugin: "kotlin-android-extensions"
    apply plugin: 'kotlin-kapt'
    apply plugin: 'dagger.hilt.android.plugin'

    android {
        compileSdkVersion 30
        buildToolsVersion "29.0.3"

        buildFeatures {
            dataBinding = true
            viewBinding = true
        }

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 30

            versionCode 1
            versionName "1.0"
            vectorDrawables.useSupportLibrary = true

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
            jvmTarget = "1.8"
        }

        configurations.all {

            resolutionStrategy {
                force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                force "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            }
        }
    }

    apply from: rootDir.path + "/dependencies.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}